(uiop:define-package #:cl-telegram-bot2/high
  (:use #:cl)
  (:import-from #:cl-telegram-bot2/api
                #:chat-title
                #:send-photo
                #:message-chat
                #:update-message
                #:update
                #:chat-id
                #:send-message)
  (:import-from #:cl-telegram-bot2/vars
                #:*current-chat*)
  (:import-from #:lambda-fiddle
                #:key-lambda-vars)
  (:import-from #:trivial-arguments
                #:arglist)
  (:import-from #:cl-telegram-bot2/errors
                #:error-description
                #:telegram-error)
  (:documentation "High level API for implementing Telegram bots.")
  (:export #:reply
           #:chat-state
           #:collect-sent-messages
           #:reply-with-photo))
(in-package #:cl-telegram-bot2/high)


;; TODO: Probably remove
(defclass chat-state ()
  ())


(defvar *collected-messages*)


(defmacro collect-sent-messages (&body body)
  "Returns as the first value a list of messages created by REPLY function called
   during BODY execution. Values returned by the BODY code are returned as the second,
   third and following arguments.

   Also, messages are collected when these actions are called:

   - CL-TELEGRAM-BOT2/ACTIONS/SEND-TEXT:SEND-TEXT
   - CL-TELEGRAM-BOT2/ACTIONS/SEND-PHOTO:SEND-PHOTO"
  `(let ((vars
           ;; This check allows us to use nested calls to collect-sent-messages.
           ;; Here inner call can see messages added during outer call,
           ;; but I don't consider this an issue for the moment.
           (unless (boundp '*collected-messages*)
             (list '*collected-messages*)))
         (vals
           (unless (boundp '*collected-messages*)
             (list nil))))
     (progv vars vals
       (let ((result-values (multiple-value-list ,@body)))
         (values-list (list* *collected-messages*
                             result-values))))))


(defmacro defun-with-same-keys ((func-name copy-kwargs-from-func) lambda-list &body body)
  "We have to use this macro, to not hardcode all possible keyword arguments from an autogenerated API functions.

   This needed primary for a convenience, because this way these arguments will be suggested by autocompletion."
  (let* ((additional-keyword-args
           (key-lambda-vars
            (arglist copy-kwargs-from-func)))
         (new-lambda-list (append lambda-list
                                  (list* '&key
                                         additional-keyword-args))))
    `(defun ,func-name ,new-lambda-list
       (declare (ignorable ,@additional-keyword-args))
       ,@body)))


(defun-with-same-keys (reply send-message)
                      (text &rest rest)
  (let* ((chat-id (chat-id *current-chat*))
         (chat-title (chat-title *current-chat*))
         (message (handler-bind
                      ((telegram-error (lambda (err)
                                         (when (string= (error-description err)
                                                        "Bad Request: need administrator rights in the channel chat")
                                           (log:warn "Unable to reply to chat ~S (~A) because bot needs administration rights on this channel"
                                                     chat-title
                                                     chat-id)
                                           (return-from reply nil)))))
                    (apply #'send-message
                           chat-id
                           text
                           rest))))
    (when (boundp '*collected-messages*)
      (push message *collected-messages*))
    (values message)))


(defun-with-same-keys (reply-with-photo send-photo)
                      (photo &rest rest)
  (let* ((chat-id (chat-id *current-chat*))
         (message (apply #'send-photo
                         chat-id
                         photo
                         rest)))
    (when (boundp '*collected-messages*)
      (push message *collected-messages*))
    (values message)))

